version = parent.version
group 'org.comroid'

apply plugin: 'java-library'
apply from: 'vars.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main.java.srcDirs = files("$parent.projectDir/src/api/java")
    main.resources.srcDirs = files("$parent.projectDir/src/api/resources")
    test.java.srcDirs = files("$parent.projectDir/src/apiTest/java")
    test.resources.srcDirs = files("$parent.projectDir/src/apiTest/resources")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'

    if (isAutoDeploy) {
        api 'org.comroid:webkit-oauth:0.+'
    } else {
        api project(':guardian:webkit:oauth')
    }

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.11.0'
}

apply from: "${parent.projectDir}/gradle/publishing.gradle"

