plugins {
    id 'war'
    id 'idea'
    id 'application'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

import static java.lang.System.getenv

ext {
    isAutoDeploy = (rootProject.name != 'repo' && rootProject.name != 'java') || getenv().containsKey("TEAMCITY") || getenv().containsKey("JITPACK")
}

group 'org.comroid'
version = parent.version
apply from: 'gradle/vars.gradle'

sourceCompatibility = 17
targetCompatibility = 17

mainClassName = 'org.comroid.auth.AuthServer'

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.session:spring-session-bom:2021.1.1'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'

    if (isAutoDeploy) {
        implementation 'org.comroid:status-api:0.+'
    } else {
        implementation project(':status:status-api')
    }

    // db dependencies
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.+'

    // spring
    implementation 'javax.servlet:jstl:1.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.springframework.security:spring-security-oauth2-authorization-server:1.+'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
