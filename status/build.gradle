tasks.findByName('wrapper').configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'application'

group 'org.comroid'
version '0.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'org.comroid.status.server.StatusServer'

configurations {
    create 'api'
    create 'web'
    main.extendsFrom api
}

sourceSets {
    main.compileClasspath += api.output
    test.java.srcDirs = [file('src/test')]
    api.java.srcDirs = [file('src/api/java')]
    web.resources.srcDirs = [file('src/web')]
}

task apiJar(type: Jar) {
    archiveBaseName = 'org.comroid.status.api'
    from sourceSets.api.output.classesDirs
}

jar {
    from sourceSets.api.output.classesDirs
}

distributions.main.contents.from apiJar
compileJava.dependsOn 'compileApiJava'

repositories {
    mavenLocal()
    maven {
        url "https://maven.jetbrains.space/comroid/repo"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    jcenter()
}

dependencies {
    // Discord4J
    implementation 'com.discord4j:discord4j-core:3.1.0.RC1'

    // BotList artifact
    implementation 'de.kaleidox:discordbotlist-stats-discord4j:0.1.3'

    // comroid utilities
    implementation 'org.comroid:common-util:1.5.0-SNAPSHOT'
    implementation 'org.comroid:uniform:1.0.0-SNAPSHOT'

    // Json Serializer
    implementation 'com.alibaba:fastjson:1.2.68'

    // Flogger
    implementation 'com.google.flogger:flogger:0.5.1'
    runtimeOnly 'com.google.flogger:flogger-system-backend:0.5.1'
}
