group 'org.comroid'
version = rootProject.version

apply plugin: 'java-library'
apply from: 'vars.gradle'
sourceCompatibility = 17
targetCompatibility = 17

task tcBuildId {
    doLast {
        println "##teamcity[buildNumber '$buildId']"
    }
}
tasks.named('jar').configure { dependsOn tcBuildId }

tasks.withType(Javadoc) {
    source = sourceSets.main.java
    options.encoding = 'UTF-8'
}

sourceSets {
    main.java.srcDirs = [file("$parent.projectDir/src/api/java")]
    main.resources.srcDirs = [file("$parent.projectDir/src/api/resources")]
    test.java.srcDirs = [file("$parent.projectDir/src/api-test/java")]
    test.resources.srcDirs = [file("$parent.projectDir/src/api-test/resources")]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'

    if (isAutoDeploy) {
        api 'org.comroid:api:0.+'
    } else {
        api project(':api')
    }

    // spring
    //noinspection VulnerableLibrariesLocal
    api 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.5'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.easymock:easymock:4.1'
}

apply from: 'publishing.gradle'
