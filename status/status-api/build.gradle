group 'org.comroid'
version = rootProject.version

apply plugin: 'java-library'
apply from: 'vars.gradle'

tasks.withType(Javadoc) {
    source = sourceSets.main.java
    options.encoding = 'UTF-8'
}

sourceSets {
    main.java.srcDirs = [file("$rootDir/src/api/java")]
    main.resources.srcDirs = [file("$rootDir/src/api/resources")]
    test.java.srcDirs = [file("$rootDir/src/api-test/java")]
    test.resources.srcDirs = [file("$rootDir/src/api-test/resources")]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'

    // usual dependencies
    implementation 'org.comroid:mutatio:0.10.1'
    implementation 'org.comroid:common-util:1.5.4'
    implementation 'org.comroid:uniform:0.16.0'
    implementation 'org.comroid:varbind:0.20.0'
    implementation 'org.comroid:restless:0.15.2'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.easymock:easymock:4.1'
    testRuntimeOnly 'com.squareup.okhttp3:okhttp:4.7.2'
    testRuntimeOnly 'com.alibaba:fastjson:1.2.70'
}

apply from: 'publishing.gradle'
